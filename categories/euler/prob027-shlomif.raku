use v6;

=begin pod

=TITLE Quadratic primes

=AUTHOR Shlomi Fish

L<https://projecteuler.net/problem=27>

Euler discovered the remarkable quadratic formula:

    n² + n + 41

It turns out that the formula will produce 40 primes for the consecutive
values n = 0 to 39. However, when n = 40, 402 + 40 + 41 = 40(40 + 1) + 41 is
divisible by 41, and certainly when n = 41, 41² + 41 + 41 is clearly
divisible by 41.

The incredible formula  n² − 79n + 1601 was discovered, which produces 80
primes for the consecutive values n = 0 to 79. The product of the
coefficients, −79 and 1601, is −126479.

Considering quadratics of the form:

    n² + an + b, where |a| < 1000 and |b| < 1000

    where |n| is the modulus/absolute value of n
    e.g. |11| = 11 and |−4| = 4

Find the product of the coefficients, a and b, for the quadratic expression
that produces the maximum number of primes for consecutive values of n,
starting with n = 0.


=end pod

sub is_prime(Int $n) returns Bool {
    if ($n <= 1) {
        return False;
    }

    for (2 .. $n.sqrt.floor) -> $i {
        if $n % $i == 0 {
            return False;
        }
    }

    return True;
}

my (Int $max_a, Int $max_b);

my Int $max_iter = 0;
for (0 .. 999) -> $b_coeff {
    for ((-$b_coeff+1) .. 999) -> $a_coeff {
        my $n = 0;
        while is_prime($b_coeff+$n*($n+$a_coeff)) {
            $n++;
        }
        $n--;

        if ($n > $max_iter) {
            ($max_a, $max_b, $max_iter) = ($a_coeff, $b_coeff, $n);
        }
    }
}
say "A sequence length of $max_iter, is generated by a=$max_a, b=$max_b, the product is {$max_a*$max_b}";

# vim: expandtab shiftwidth=4 ft=perl6
